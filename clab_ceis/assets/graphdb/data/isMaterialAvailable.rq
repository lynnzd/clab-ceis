PREFIX : <http://www.semanticweb.org/sophi/ontologies/2024/10/untitled-ontology-20/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT 
  ?recipe ?fabricBlockDesign ?requiredAmount ?availableAmount 
  (IF(?availableAmount >= ?requiredAmount, "Yes", "No") AS ?readyForAssembly)
WHERE {
  # Link recipe to its requirement
  ?recipe :hasRequirement ?requirement .
  
  # Link requirement to a specific FabricBlockDesign and its required amount
  ?requirement :requiresFabricBlockDesign ?fabricBlockDesign .
  ?requirement :fabricBlockAmount ?requiredAmount .
  
  # Count the available FabricBlocks for the specified FabricBlockDesign
  {
    SELECT ?fabricBlockDesign (COUNT(?fabricBlock) AS ?availableAmount)
    WHERE {
      ?fabricBlock rdf:type :FabricBlock .
      ?fabricBlock :followsFabricBlockDesign ?fabricBlockDesign .
    }
    GROUP BY ?fabricBlockDesign
  }
}